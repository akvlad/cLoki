request ::= <log_stream_selector> | <log_range_aggregation> | <aggregation_operator>

log_stream_selector ::= "{" <log_stream_selector_rule> <opt_log_stream_selector_rule> "}" <opt_log_pipeline>

log_stream_selector_rule ::= <label> <operator> <quoted_str>
label ::= /[a-zA-Z0-9_]+(?![a-zA-Z0-9_])/
operator ::= /(!=|=~|!~|=)/
quoted_str ::= "\"" <escaped string with correct \\ and \" escaping> "\""
opt_log_stream_selector_rule ::= "," <log_stream_selector_rule> <opt_log_stream_selector_rule> | ""

opt_log_pipeline ::= <log_pipeline> <opt_log_pipeline> | ""
log_pipeline ::= <line_filter_expression> | <parser_expression> | <label_filter_expression> | <line_format_expression> | <labels_format_expression> | <unwrap_expression>

line_filter_expression ::= <line_filter_operator> <quoted_str>
line_filter_operator ::= /(\|=|!=|\|~|!~)/

parser_expression ::= "|" <parser_fn_name> <opt_parameters>
parser_fn_name ::= "json"|"logfmt"|"regexp"|"unpack"|<optional plugable functions from some fn registry>
opt_parameters ::=  <parameter> <opt_comma_separated_parameters> | ""
parameter ::= <label> "=" <quoted_str> | <quoted_str>
opt_comma_separated_parameters ::= "," <parameter> <opt_comma_separated_parameters> | ""

label_filter_expression ::= "|" <string_label_filter_expression> | "|" <number_label_filter_expression> 

string_label_filter_expression ::= <label> <operator> <string_label_filter_expression_right_hand>
string_label_filter_expression_right_hand ::= <quoted_str> | <backticked_str>
backticked_str ::= "`" <escaped string with correct \\ and \` escaping> "`"

number_label_filter_expression ::= <label> <number_operator> <number_value>
number_operator ::= /(==|!=|>|>=|<|<=)/
number_value ::= <duration_value> | <bytes_value> | <floating_point_value>
duration_value ::= <floating_point_value> /(ns|us|ms|s|m|h)/
bytes_value ::= <floating_point_value> /(b|kib|kb|mb|mib|gib|gb|lib|tb|pib|pb|eib|eb)/
floating_point_value ::= /[0-9]+\.?[0-9]+/

line_format_expression ::= "| line_format" <string_label_filter_expression_right_hand>

labels_format_expression ::= "| label_format" <labels_format_expression_param> <opt_labels_format_expression_param>
labels_format_expression_param ::= <label_rename_param> | <label_inject_param>
label_rename_param ::= <label> "=" <label>
label_inject_param ::= <label> "=" <string_label_filter_expression_right_hand>
opt_labels_format_expression_param ::= "," <labels_format_expression_param> <opt_labels_format_expression_param> | ""

log_range_aggregation ::= <log_range_aggregation_fn> "(" <log_stream_selector> "[" <duration_value> "]" ")"
log_range_aggregation_fn ::= "rate"|"count_over_time"|"bytes_rate"|"bytes_over_time"|"absent_over_time"|<optional plugable functions from some fn registry>

aggregation_operator ::= <aggregation_operator_fn> "(" <opt_aggregation_operator_param> <log_range_aggregation> <opt_by_without> ")"
aggregation_operator_fn ::= "sum"|"min"|"max"|"avg"|"stddev"|"stdvar"|"count"|"bottomk"|"topk"|<optional plugable functions from some fn registry>
opt_aggregation_operator_param ::= /[0-9]+ *,/ | ""
opt_by_without ::= <by_without> "(" <label_list> ")" | ""
by_without ::= "by" | "without"
label_list ::= <label> <opt_comma_separated_labels>
opt_comma_separated_labels ::= "," <label> <opt_comma_separated_labels> | ""
